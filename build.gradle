plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

group = 'me.defineoutside'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven { url 'https://repo.codemc.io/repository/maven-snapshots/' }
}

dependencies {
    compileOnly 'org.bukkit:bukkit:1.12.2-R0.1-SNAPSHOT'
    //Keeps PacketEvents constantly up to date (latest commit)
    implementation group: "com.github.retrooper", name: "packetevents-spigot", version: "2.3.1-SNAPSHOT"
    //implementation 'com.github.retrooper.packetevents:spigot:2.0.0-SNAPSHOT'
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

configurations.all {
    // Check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

shadowJar {
    minimize()
    relocate('com.github.retrooper.packetevents', 'me.define.packeteventstest.packetevents.api')
    relocate('io.github.retrooper.packetevents', 'me.define.packeteventstest.packetevents.impl')
}
